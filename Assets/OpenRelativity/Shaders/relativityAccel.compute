// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

//Prevent NaN and Inf
#define divByZeroCutoff 1e-8f

//#define quaternion float4
//
//inline quaternion fromToRotation(float3 from, float3 to) {
//	quaternion rotation;
//	rotation.xyz = cross(from, to);
//	rotation.w = sqrt(dot(from, from) + dot(to, to) + dot(from, to));
//	return normalize(rotation);
//}
//
////See: https://blog.molecular-matters.com/2013/05/24/a-faster-quaternion-vector-multiplication/
//inline float3 rotate(quaternion rot, float3 vec) {
//	float3 temp;
//	temp = 2 * cross(rot.xyz, vec.xyz);
//	return vec + rot.w * temp + cross(rot.xyz, temp);
//}
//
//inline quaternion inverse(quaternion q) {
//	return quaternion(-q.xyz, q.w) / length(q);
//}

struct GlobalParams {
	float4x4 ltwMatrix; //local-to-world transform matrix
	float4x4 wtlMatrix; //world-to-local transform matrix
	float4 viw; //velocity of object in synchronous coordinates
	float4 vpc; //velocity of player
	float4 playerOffset; //player position in world
	float spdOfLight; //current speed of light
	float4 pap; //acceleration of player in world coordinates
	float4 avp; //angular velocity of player in world coordinates
	float4 aiw; //acceleration of object in world coordinates
	float4x4 vpcLorentzMatrix; //Lorentz transform between world and player
	float4x4 viwLorentzMatrix; //Lorentz transform between world and object
	float4x4 invVpcLorentzMatrix; //Lorentz transform between player and world
	float4x4 invViwLorentzMatrix; //Lorentz transform between object and world
};

StructuredBuffer<GlobalParams> glblPrms;
RWStructuredBuffer<float3> verts;

[numthreads(256, 1, 1)]
//[numthreads(64,1,1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
	float4x4 ltwMatrix = glblPrms[0].ltwMatrix;
	float4x4 wtlMatrix = glblPrms[0].wtlMatrix;
	float4 viw = glblPrms[0].viw;
	float4 vpc = glblPrms[0].vpc;
	float4 playerOffset = glblPrms[0].playerOffset;
	float spdOfLight = glblPrms[0].spdOfLight;
	float4 pap = glblPrms[0].pap;
	float4 avp = glblPrms[0].avp;
	float4 aiw = glblPrms[0].aiw;
	float4x4 vpcLorentzMatrix = glblPrms[0].vpcLorentzMatrix;
	float4x4 viwLorentzMatrix = glblPrms[0].viwLorentzMatrix;
	float4x4 invVpcLorentzMatrix = glblPrms[0].invVpcLorentzMatrix;
	float4x4 invViwLorentzMatrix = glblPrms[0].invViwLorentzMatrix;

	float4 pos = float4(verts[id.x].xyz, 0);
	float4 tempPos = mul(ltwMatrix, float4(pos.xyz, 1.0f));
	pos = float4(tempPos.xyz / tempPos.w - playerOffset.xyz, 0);

	float speed = length(vpc.xyz);
	float spdOfLightSqrd = spdOfLight * spdOfLight;

	//riw = location in world, for reference
	float4 riw = float4(pos.xyz, 0); //Position that will be used in the output

	//Boost to rest frame of player:
	float4 riwForMetric = mul(vpcLorentzMatrix, riw);

	//Find metric based on player acceleration and rest frame:
	float linFac = 1 + dot(pap.xyz, riwForMetric.xyz) / spdOfLightSqrd;
	linFac *= linFac;
	float angFac = dot(avp.xyz, riwForMetric.xyz) / spdOfLight;
	angFac *= angFac;
	float avpMagSqr = dot(avp.xyz, avp.xyz);
	float3 angVec = float3(0, 0, 0);
	if (avpMagSqr > divByZeroCutoff) {
		angVec = 2 * angFac / (spdOfLight * avpMagSqr) * avp.xyz;
	}

	float4x4 metric = {
		-1, 0, 0, -angVec.x,
		0, -1, 0, -angVec.y,
		0, 0, -1, -angVec.z,
		-angVec.x, -angVec.y, -angVec.z, (linFac * (1 - angFac) - angFac)
	};

	//Lorentz boost back to world frame;
	metric = mul(transpose(invVpcLorentzMatrix), mul(metric, invVpcLorentzMatrix));

	//Remember that relativity is time-translation invariant.
	//The above metric gives the numerically correct result if the time coordinate of riw is zero,
	//(at least if the "conformal factor" or "mixed [indices] metric" is the identity).
	//We are free to translate our position in time such that this is the case.

	//Apply Lorentz transform;
	metric = mul(transpose(viwLorentzMatrix), mul(metric, viwLorentzMatrix));
	float4 aiwTransformed = mul(viwLorentzMatrix, aiw);
	//Translate in time:
	float4 riwTransformed = mul(viwLorentzMatrix, riw);
	float tisw = riwTransformed.w;
	riwTransformed.w = 0;

	//(When we "dot" four-vectors, always do it with the metric at that point in space-time, like we do so here.)
	float riwDotRiw = -dot(riwTransformed, mul(metric, riwTransformed));
	float aiwDotAiw = -dot(aiwTransformed, mul(metric, aiwTransformed));
	float riwDotAiw = -dot(riwTransformed, mul(metric, aiwTransformed));

	float sqrtArg = riwDotRiw * (spdOfLightSqrd - riwDotAiw + aiwDotAiw * riwDotRiw / (4 * spdOfLightSqrd)) / ((spdOfLightSqrd - riwDotAiw) * (spdOfLightSqrd - riwDotAiw));
	float aiwMag = length(aiwTransformed.xyz);
	float t2 = 0;
	if (sqrtArg > 0)
	{
		t2 = -sqrt(sqrtArg);
	}
	tisw += t2;
	//add the position offset due to acceleration
	if (aiwMag > divByZeroCutoff)
	{
		riwTransformed.xyz -= aiwTransformed.xyz / aiwMag * spdOfLightSqrd * (sqrt(1 + (aiwMag * t2 / spdOfLight) * (aiwMag * t2 / spdOfLight)) - 1);
	}
	riwTransformed.w = tisw;

	//Inverse Lorentz transform the position:
	riw = mul(invViwLorentzMatrix, riwTransformed);
	tisw = riw.w;

	riw = float4(riw.xyz + tisw * spdOfLight * viw.xyz, 0);

	float newz = speed * spdOfLight * tisw;

	if (speed > divByZeroCutoff) {
		float3 vpcUnit = vpc.xyz / speed;
		newz = (dot(riw.xyz, vpcUnit) + newz) / (float)sqrt(1 - (speed * speed));
		riw += (newz - dot(riw.xyz, vpcUnit)) * float4(vpcUnit, 0);
	}

	riw += float4(playerOffset.xyz, 0);

	//Transform the vertex back into local space for the mesh to use it
	tempPos = mul(wtlMatrix, float4(riw.xyz, 1.0f));
	verts[id.x] = float3(tempPos.x, tempPos.y, tempPos.z) / tempPos.w;
}